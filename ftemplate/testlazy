#!/usr/bin/perl -w

use strict; use warnings FATAL => 'uninitialized';

use lib "./lib";
use Chj::PXHTML ':all';
use Chj::FP2::Lazy;
use Chj::FP2::List;
use Chj::PXML::Serialize;

use utf8;

$|=1;

sub countdown {
    my ($i)=@_;
    DelayLight {
	#sleep 1;
	if ($i >= 0) {
	    cons (P($i),
		  countdown ($i-1));
	} else {
	    undef
	}
    }
}

sub page {
    my ($title,$mtime,$main)= @_;
    HTML(
	HEAD(
	    TITLE($title)),
	BODY(
	    $main,
	    HR(),
	    P(
		"By ", A({href=> "http://christianjaeger.ch"},
			 "Christian Jaeger"),
		", last modified at ", localtime($mtime)."", " (or something).")))
}

our $numbers= {
	       1=> "one",
	       2=> "two",
	       3=> "three"};

sub examplepage {
    my ($title)= @_;
    page("example page - $title",
	 time,
	 [
	  H1($title),
	  P("Garçon méchanique, \"1 < 2\" is true. ",
	    A({href=> "\"1 < 2\""},
	      "this will be 404")),
	  TABLE({border=>1},
		map {
		    TR(TD($_), TD($$numbers{$_}))
		} (1..3)),
	  countdown (1e9), #countdown (100),
	 ])
}

pxml_xhtml_print examplepage ("Hello World"), *STDOUT{IO}, "en";

